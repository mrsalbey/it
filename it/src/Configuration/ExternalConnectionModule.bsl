Перем КаталогXSLПреобразований Экспорт;

// Получить список справочников в формате XML и преобразовать его к HTML
// при помощи XSL-трансформации.
Функция getRefList() Экспорт
	РезультатXML = "";
	Писатель = Новый XMLWriter;
	Писатель.УстановитьСтроку();
	Писатель.ЗаписатьОбъявлениеXML();
	Писатель.ЗаписатьНачалоЭлемента("references");
	Нум = 1;
	Для Каждого Спр Из Метаданные.Справочники Цикл
		Писатель.ЗаписатьНачалоЭлемента("reference");
		Писатель.ЗаписатьНачалоЭлемента("number");
		Писатель.ЗаписатьТекст("" + Нум);
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьНачалоЭлемента("name");
		Писатель.ЗаписатьТекст("" + Спр);
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьКонецЭлемента();
		Нум = Нум + 1;
	КонецЦикла;
	Писатель.ЗаписатьКонецЭлемента();
	РезультатXML = Писатель.Закрыть();

	Трансформатор = Новый ПреобразованиеXSL;
	Трансформатор.ЗагрузитьИзФайла(КаталогXSLПреобразований + "/reflist.xsl");
	РезультатHTML = Трансформатор.ПреобразоватьИзСтроки(РезультатXML);

	Возврат РезультатHTML;
КонецФункции

// Получить список элементов справочника по текстовому представлению
// ссылки на родительский элемент.
Функция getRefOne(НомерСправочника, СсылкаНаЗапись) Экспорт
	СтрокаСоСсылкой = "{""#""," + СсылкаНаЗапись + "}";
	МдСпр = Метаданные.Справочники[Число(НомерСправочника) - 1];
	Спр = Справочники[МдСпр.Имя];
	Если СсылкаНаЗапись = неопределено или СсылкаНаЗапись = "" Тогда
		Выборка = Спр.выбрать(Спр.ПустаяСсылка());
	Иначе
		Выборка = Спр.выбрать(ЗначениеИзСтрокиВнутр(СтрокаСоСсылкой));
	КонецЕсли;

	РезультатXML = "";
	Писатель = Новый XMLWriter;
	Писатель.УстановитьСтроку();
	Писатель.ЗаписатьОбъявлениеXML();
	Писатель.ЗаписатьНачалоЭлемента("reference");
	Писатель.ЗаписатьНачалоЭлемента("name");
	Писатель.ЗаписатьТекст("" + МдСпр);
	Писатель.ЗаписатьКонецЭлемента();
	Писатель.ЗаписатьНачалоЭлемента("code");
	Писатель.ЗаписатьТекст("" + НомерСправочника);
	Писатель.ЗаписатьКонецЭлемента();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа Тогда
			Писатель.ЗаписатьНачалоЭлемента("group");
		Иначе
			Писатель.ЗаписатьНачалоЭлемента("item");
		КонецЕсли;
		Писатель.ЗаписатьНачалоЭлемента("id");
		СтрокаСоСсылкой = ЗначениеВСтрокуВнутр(Выборка.ссылка);
		Писатель.ЗаписатьТекст(сред(СтрокаСоСсылкой, 6, СтрДлина(СтрокаСоСсылкой) - 6));
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьНачалоЭлемента("code");
		Писатель.ЗаписатьТекст("" + Выборка.Код);
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьНачалоЭлемента("description");
		Писатель.ЗаписатьТекст(Выборка.Наименование);
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьКонецЭлемента();
	КонецЦикла;
	Писатель.ЗаписатьКонецЭлемента();
	РезультатXML = Писатель.Закрыть();

	Трансформатор = Новый ПреобразованиеXSL;
	Трансформатор.ЗагрузитьИзФайла(КаталогXSLПреобразований + "/refone.xsl");
	РезультатHTML = Трансформатор.ПреобразоватьИзСтроки(РезультатXML);

	Возврат РезультатHTML;
КонецФункции

// Получить значения всех реквизитов и табличных частей одного элемента
// справочника по текстовому представлению ссылки на него.
Функция getRefItem(НомерСправочника, СсылкаНаЗапись) Экспорт
	МдСпр = Метаданные.Справочники[Число(НомерСправочника) - 1];
	СтрокаСоСсылкой = "{""#""," + СсылкаНаЗапись + "}";

	Запись = ЗначениеИзСтрокиВнутр(СтрокаСоСсылкой);

	РезультатXML = "";
	Писатель = Новый XMLWriter;
	Писатель.УстановитьСтроку();
	Писатель.ЗаписатьОбъявлениеXML();
	Писатель.ЗаписатьНачалоЭлемента("item");
	Писатель.ЗаписатьНачалоЭлемента("name");
	Писатель.ЗаписатьТекст(Запись.Наименование);
	Писатель.ЗаписатьКонецЭлемента();
	Писатель.ЗаписатьНачалоЭлемента("prop");
		Писатель.ЗаписатьНачалоЭлемента("name");
		Писатель.ЗаписатьТекст("Код");
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьНачалоЭлемента("value");
		Писатель.ЗаписатьТекст("" + Запись.Код);
		Писатель.ЗаписатьКонецЭлемента();
	Писатель.ЗаписатьКонецЭлемента();
	Писатель.ЗаписатьНачалоЭлемента("prop");
		Писатель.ЗаписатьНачалоЭлемента("name");
		Писатель.ЗаписатьТекст("Наименование");
		Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьНачалоЭлемента("value");
		Писатель.ЗаписатьТекст(Запись.Наименование);
		Писатель.ЗаписатьКонецЭлемента();
	Писатель.ЗаписатьКонецЭлемента();
	Для Каждого Рекв Из МдСпр.Реквизиты Цикл
		Писатель.ЗаписатьНачалоЭлемента("prop");
			Писатель.ЗаписатьНачалоЭлемента("name");
			Писатель.ЗаписатьТекст("" + Рекв);
			Писатель.ЗаписатьКонецЭлемента();
			Писатель.ЗаписатьНачалоЭлемента("value");
			Писатель.ЗаписатьТекст("" + Запись[Рекв.Имя]);
			Писатель.ЗаписатьКонецЭлемента();
		Писатель.ЗаписатьКонецЭлемента();
	КонецЦикла;
	Для Каждого МдТч Из МдСпр.ТабличныеЧасти Цикл
		Писатель.ЗаписатьНачалоЭлемента("table");
			Писатель.ЗаписатьНачалоЭлемента("name");
			Писатель.ЗаписатьТекст("" + МдТч);
			Писатель.ЗаписатьКонецЭлемента();
			Писатель.ЗаписатьНачалоЭлемента("header");
				Для Каждого МдРеквТч Из МдТч.Реквизиты Цикл
					Писатель.ЗаписатьНачалоЭлемента("name");
					Писатель.ЗаписатьТекст("" + МдРеквТч);
					Писатель.ЗаписатьКонецЭлемента();
				КонецЦикла;
			Писатель.ЗаписатьКонецЭлемента();
			Тч = Запись[МдТч.Имя];
			Для Каждого ЗаписьТч Из Тч Цикл
				Писатель.ЗаписатьНачалоЭлемента("record");
					Для Каждого МдРеквТч Из МдТч.Реквизиты Цикл
						Писатель.ЗаписатьНачалоЭлемента("value");
						Писатель.ЗаписатьТекст("" + ЗаписьТч[МдРеквТч.Имя]);
						Писатель.ЗаписатьКонецЭлемента();
					КонецЦикла;
				Писатель.ЗаписатьКонецЭлемента();
			КонецЦикла;
		Писатель.ЗаписатьКонецЭлемента();
	КонецЦикла;
	Писатель.ЗаписатьКонецЭлемента();
	РезультатXML = Писатель.Закрыть();

	Трансформатор = Новый ПреобразованиеXSL;
	Трансформатор.ЗагрузитьИзФайла(КаталогXSLПреобразований + "/refitem.xsl");
	РезультатHTML = Трансформатор.ПреобразоватьИзСтроки(РезультатXML);

	Возврат РезультатHTML;
КонецФункции

// Исполнить запрос, построить его результат в XML-формате и преобразовать его
// в HTML, используя XSL-преобразование, указанное в запросе.
Функция getRequest(ТекстЗапроса) Экспорт
	Длина = найти(ТекстЗапроса, ";");
	Если Длина = 0 Тогда
		ТекстЗапросаБезФайла = ТекстЗапроса;
		ФайлТрансформатор = КаталогXSLПреобразований + "/request2.xsl";
	Иначе
		ТекстЗапросаБезФайла = сред(ТекстЗапроса, 1, Длина - 1);
		ФайлТрансформатор = КаталогXSLПреобразований + "/" + сред(ТекстЗапроса, Длина + 1, СтрДлина(ТекстЗапроса) - Длина);
	КонецЕсли;

	Запр = Новый Запрос;
	Запр.Текст = ТекстЗапросаБезФайла;
	Результат = Запр.Выполнить();

	РезультатXML = "";
	Писатель = Новый XMLWriter;
	Писатель.УстановитьСтроку();
	Писатель.ЗаписатьОбъявлениеXML();
	Писатель.ЗаписатьНачалоЭлемента("result");

	СтрокаРезультата = Результат.Выбрать();
	Пока СтрокаРезультата.Следующий() Цикл
		Писатель.ЗаписатьНачалоЭлемента("record");
		Для Каждого Столбец Из Результат.Колонки Цикл
			Если не Столбец.ТипЗначения.СодержитТип(тип("ТаблицаЗначений")) Тогда
				Писатель.ЗаписатьНачалоЭлемента(Столбец.Имя);
				Писатель.ЗаписатьТекст("" + СтрокаРезультата[Столбец.Имя]);
				Писатель.ЗаписатьКонецЭлемента();
		    Иначе
				Писатель.ЗаписатьНачалоЭлемента(Столбец.Имя);
				ТабличнаяЧасть = СтрокаРезультата[Столбец.Имя];
				СтрокаТЧ = ТабличнаяЧасть.выбрать();
				Пока СтрокаТЧ.Следующий() Цикл
					Писатель.ЗаписатьНачалоЭлемента("record");
					Для Каждого СтолбецТЧ Из ТабличнаяЧасть.Колонки Цикл
						Писатель.ЗаписатьНачалоЭлемента(СтолбецТЧ.Имя);
						Писатель.ЗаписатьТекст("" + СтрокаТЧ[СтолбецТЧ.Имя]);
						Писатель.ЗаписатьКонецЭлемента();
					КонецЦикла;
					Писатель.ЗаписатьКонецЭлемента();
				КонецЦикла;
				Писатель.ЗаписатьКонецЭлемента();
			КонецЕсли;
		КонецЦикла;
		Писатель.ЗаписатьКонецЭлемента();
	КонецЦикла;

    Писатель.ЗаписатьКонецЭлемента();
	РезультатXML = Писатель.Закрыть();

	Трансформатор = Новый ПреобразованиеXSL;
	Трансформатор.ЗагрузитьИзФайла(ФайлТрансформатор);
	РезультатHTML = Трансформатор.ПреобразоватьИзСтроки(РезультатXML);

	Возврат РезультатHTML;
КонецФункции

// Вставить Документ, представленный в XML-формате.
Процедура insertDoc(СтрокаXML) Экспорт
	Читатель = Новый XMLReader;
	Читатель.УстановитьСтроку(СтрокаXML);
	ПропуститьДоЭлемента("", Читатель);
	ИмяУзла = Читатель.Имя;
	МдДок = Метаданные.Документы[ИмяУзла];
	ПрочитатьДокумент(Читатель);
КонецПроцедуры

// Распознать Документ и вставить его в таблицу соответствующих Документов.
Процедура ПрочитатьДокумент(Читатель)
	ИмяДокумента = Читатель.Имя;
	Документ = Документы[ИмяДокумента].СоздатьДокумент();
	Пока ПрочитатьРеквизитИлиТЧ(ИмяДокумента, Документ, Читатель) Цикл
	КонецЦикла;
	Документ.Записать();
КонецПроцедуры

// Распознать один реквизит или одну табличную часть Документа.
Функция ПрочитатьРеквизитИлиТЧ(ИмяДокумента, Документ, Читатель)
	Если не Читатель.Прочитать() Тогда
		Возврат ложь;
	КонецЕсли;
	Если не ПропуститьДоЭлемента(ИмяДокумента, Читатель) Тогда
		Возврат ложь;
	КонецЕсли;
	ИмяПоля = Читатель.Имя;
	Попытка
		Если найти(строка(Документ[ИмяПоля]), "ДокументТабличнаяЧасть") > 0 Тогда
			// Заполнить табличную часть Документа.
			Тч = Документ[ИмяПоля];
			ПрочитатьТабличнуюЧасть(ИмяПоля, Тч, Читатель);
		Иначе
			// Заполнить реквизит Документа.
			Если ПропуститьДоТекста(ИмяПоля, Читатель) Тогда
				Значение = Читатель.Значение;
				МдДок = Метаданные.Документы[ИмяДокумента];
				Если ИмяПоля <> "Номер" или не МдДок.КонтрольУникальности Тогда
					СтрокаТипа = строка(ТипЗнч(Документ[ИмяПоля]));
					Если найти(СтрокаТипа, "Справочник ссылка") > 0 Тогда
						Поз = найти(СтрокаТипа, ": ");
						Спр = сред(СтрокаТипа, Поз + 2, СтрДлина(СтрокаТипа) - Поз - 1);
						ЭлСпр = Справочники[Спр].НайтиПоНаименованию(Значение);
						Документ[ИмяПоля] = ЭлСпр;
					ИначеЕсли найти(СтрокаТипа, "Документ ссылка") > 0 Тогда
						Поз = найти(СтрокаТипа, ": ");
						Док = сред(СтрокаТипа, Поз + 2, СтрДлина(СтрокаТипа) - Поз - 1);
						ЭлДок = Справочники[Док].НайтиПоНаименованию(Значение);
						Документ[ИмяПоля] = ЭлДок;
					Иначе
						Документ[ИмяПоля] = Значение;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Исключение
	КонецПопытки;
	Если не ПропуститьДоКонцаЭлемента(ИмяПоля, Читатель) Тогда
		Возврат ложь;
	КонецЕсли;
	Возврат истина;
КонецФункции

// Пропустить содержимое XML-Документа до ближайшего начала элемента.
Функция ПропуститьДоЭлемента(ИмяЭлемента, Читатель)
	Пока Читатель.ТипУзла <> ТипУзлаXML.НачалоЭлемента Цикл
		Если Читатель.ТипУзла = ТипУзлаXML.КонецЭлемента и
			 Читатель.Имя = ИмяЭлемента Тогда
			Возврат ложь;
		КонецЕсли;
		Если не Читатель.Прочитать() Тогда
			Возврат ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат истина;
КонецФункции

// Пропустить содержимое XML-Документа до текста текущего элемента.
Функция ПропуститьДоТекста(ИмяЭлемента, Читатель)
	Пока Читатель.ТипУзла <> ТипУзлаXML.Текст Цикл
		Если не Читатель.Прочитать() Тогда
			Возврат ложь;
		КонецЕсли;
		Если Читатель.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = Читатель.Имя;
			Пока Читатель.ТипУзла <> ТипУзлаXML.КонецЭлемента или 
				 Читатель.Имя <> ИмяЭлемента Цикл
				Если не Читатель.Прочитать() Тогда
					Возврат ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если Читатель.ТипУзла = ТипУзлаXML.КонецЭлемента и 
			 Читатель.Имя = ИмяЭлемента Тогда
			Возврат ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат истина;
КонецФункции

// Пропустить содержимое XML-документа до конца указанного элемента.
Функция ПропуститьДоКонцаЭлемента(имяЭлемента, Читатель)
	Пока Читатель.ТипУзла <> ТипУзлаXML.КонецЭлемента или 
		 Читатель.Имя <> ИмяЭлемента Цикл
		Если не Читатель.Прочитать() Тогда
			Возврат ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат истина;
КонецФункции

// Прочитать все записи табличной части документа.
Процедура ПрочитатьТабличнуюЧасть(ИмяЭлемента, Тч, Читатель)
	Если не Читатель.Прочитать() Тогда
		Возврат;
	КонецЕсли;
	Если не ПропуститьДоЭлемента(ИмяЭлемента, Читатель) Тогда
		Возврат;
	КонецЕсли;
	Пока ПрочитатьЗаписьТЧ(ИмяЭлемента, Тч, Читатель) Цикл
	КонецЦикла;
КонецПроцедуры

// Прочитать одну запись табличной части документа.
Функция ПрочитатьЗаписьТЧ(ИмяТЧ, ТЧ, Читатель)
	Если не ПропуститьДоЭлемента(ИмяТЧ, Читатель) Тогда
		Возврат ложь;
	КонецЕсли;
	ИмяЭлемента = Читатель.Имя;
	СтрокаТЧ = ТЧ.Добавить();
	Пока ПрочитатьРеквизитТЧ(ИмяЭлемента, СтрокаТЧ, Читатель) Цикл
	КонецЦикла;
	Возврат истина;
КонецФункции

// Прочитать один реквизит табличной части документа.
Функция ПрочитатьРеквизитТЧ(ИмяЭлемента, СтрокаТЧ, Читатель)
	Если не Читатель.Прочитать() Тогда
		Возврат ложь;
	КонецЕсли;
	Если не ПропуститьДоЭлемента(ИмяЭлемента, Читатель) Тогда
		Возврат ложь;
	КонецЕсли;
	ИмяПоля = Читатель.Имя;
	Попытка
		// Заполнить реквизит Документа.
		Если ПропуститьДоТекста(ИмяПоля, Читатель) Тогда
			Значение = Читатель.Значение;
			СтрокаТЧ[ИмяПоля] = Значение;
		КонецЕсли;
	Исключение
	КонецПопытки;
	Если не ПропуститьДоКонцаЭлемента(ИмяПоля, Читатель) Тогда
		Возврат ложь;
	КонецЕсли;
	Возврат истина;
КонецФункции

// Получить приходную накладную по номеру.
Функция ПолучитьПриходнуюНакладную(Номер) Экспорт
	Запр = Новый Запрос;
	Запр.Текст = "Выбрать * Из Документ.ПриходнаяНакладная Где Номер = """ + Номер + """";
	Результат = Запр.Выполнить();

	РезультатXML = "";
	Писатель = Новый XMLWriter;
	Писатель.УстановитьСтроку();
	Писатель.ЗаписатьОбъявлениеXML();

	СтрокаРезультата = Результат.Выбрать();
	Пока СтрокаРезультата.Следующий() Цикл
		Писатель.ЗаписатьНачалоЭлемента("ПриходнаяНакладная");
		Для Каждого Столбец Из Результат.Колонки Цикл
			Если не Столбец.ТипЗначения.СодержитТип(тип("ТаблицаЗначений")) Тогда
				Писатель.ЗаписатьНачалоЭлемента(Столбец.Имя);
				Писатель.ЗаписатьТекст("" + СтрокаРезультата[Столбец.Имя]);
				Писатель.ЗаписатьКонецЭлемента();
		    Иначе
				Писатель.ЗаписатьНачалоЭлемента(Столбец.Имя);
				ТабличнаяЧасть = СтрокаРезультата[Столбец.Имя];
				СтрокаТЧ = ТабличнаяЧасть.выбрать();
				Пока СтрокаТЧ.Следующий() Цикл
					Писатель.ЗаписатьНачалоЭлемента("record");
					Для Каждого СтолбецТЧ Из ТабличнаяЧасть.Колонки Цикл
						Писатель.ЗаписатьНачалоЭлемента(СтолбецТЧ.Имя);
						Писатель.ЗаписатьТекст("" + СтрокаТЧ[СтолбецТЧ.Имя]);
						Писатель.ЗаписатьКонецЭлемента();
					КонецЦикла;
					Писатель.ЗаписатьКонецЭлемента();
				КонецЦикла;
				Писатель.ЗаписатьКонецЭлемента();
			КонецЕсли;
		КонецЦикла;
		Писатель.ЗаписатьКонецЭлемента();
	КонецЦикла;

	РезультатXML = Писатель.Закрыть();

	Возврат РезультатXML;
КонецФункции
