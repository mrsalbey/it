
// Получить таблицу .
//
Функция ПолучитьТаблицуТаблицаСписанныхОС(СтруктураШапкиДокумента) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеОСОсновныеСредства.Ссылка КАК Регистратор,
	               |	СписаниеОСОсновныеСредства.Ссылка.Дата КАК ДатаСписания,
	               |	СписаниеОСОсновныеСредства.ОсновноеСредство
	               |ИЗ
	               |	Документ.СписаниеОС.ОсновныеСредства КАК СписаниеОСОсновныеСредства
	               |ГДЕ
	               |	СписаниеОСОсновныеСредства.Ссылка = &Ссылка";
	
	 Запрос.УстановитьПараметр("Ссылка",СтруктураШапкиДокумента.Ссылка);
	 Возврат(Запрос.Выполнить().Выгрузить());
	 
КонецФункции // ПолучитьТаблицуКомплектующих()


Функция ПолучитьТаблицуРаспределениеЛицензий(ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |	СписаниеОСОсновныеСредства.ОсновноеСредство
				   |ПОМЕСТИТЬ втКомпьютеры
				   |ИЗ
				   |	Документ.СписаниеОС.ОсновныеСредства КАК СписаниеОСОсновныеСредства
				   |ГДЕ
				   |	СписаниеОСОсновныеСредства.Ссылка = &Регистратор
				   |;
				   |
				   |////////////////////////////////////////////////////////////////////////////////
				   |ВЫБРАТЬ
				   |	РазмещениеЛицензийОстатки.ИдентификаторКомпьютера,
				   |	РазмещениеЛицензийОстатки.ИдентификаторПрограммы,
				   |	РазмещениеЛицензийОстатки.КоличествоОстаток КАК Количество,
				   |	&Регистратор,
				   |	&Период,
				   |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
				   |	ИСТИНА КАК Активность,
				   |	РазмещениеЛицензийОстатки.Организация,
				   |	РазмещениеЛицензийОстатки.ПодразделениеОрганизации,
				   |	РазмещениеЛицензийОстатки.ИдентификаторУстановлено,
				   |	РазмещениеЛицензийОстатки.Лицензия
				   |ИЗ
				   |	РегистрНакопления.РазмещениеЛицензий.Остатки(
				   |			&МоментВремени,
				   |			ИдентификаторПрограммы.ПризнакУчета = ЗНАЧЕНИЕ(Перечисление.ПризнакУчетаПрограмм.Учитывать)
				   |				И ИдентификаторКомпьютера В
				   |					(ВЫБРАТЬ
				   |						втКомпьютеры.ОсновноеСредство
				   |					ИЗ
				   |						втКомпьютеры КАК втКомпьютеры)) КАК РазмещениеЛицензийОстатки";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(ДополнительныеСвойства.ДляПроведения.Ссылка.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Регистратор", ДополнительныеСвойства.ДляПроведения.Ссылка);
	Запрос.УстановитьПараметр("Период", ДополнительныеСвойства.ДляПроведения.Дата);
	
	Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	
	Возврат(Запрос.Выполнить().Выгрузить());
	
 КонецФункции //ПолучитьТаблицуРаспределениеЛицензий	
 
 
Функция ПолучитьТаблицуРазмещенияОбъектов(ДополнительныеСвойства)	Экспорт
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	СписаниеОСОсновныеСредства.ОсновноеСредство
					|ПОМЕСТИТЬ втКомпьютеры
					|ИЗ
					|	Документ.СписаниеОС.ОсновныеСредства КАК СписаниеОСОсновныеСредства
					|ГДЕ
					|	СписаниеОСОсновныеСредства.Ссылка = &Регистратор
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	РазмещениеОбъектовОстатки.ИдентификаторКомпьютера,
					|	РазмещениеОбъектовОстатки.ИдентификаторПрограммы,
					|	РазмещениеОбъектовОстатки.ОбъектКомпьютера,
					|	РазмещениеОбъектовОстатки.КоличествоОстаток КАК Количество,
					|	&Регистратор,
					|	&Период,
					|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
					|	ИСТИНА КАК Активность,
					|	РазмещениеОбъектовОстатки.ИдентификаторДомена
					|ИЗ
					|	РегистрНакопления.РазмещениеОбъектов.Остатки(
					|			&МоментВремени,
					|			ИдентификаторКомпьютера В
					|				(ВЫБРАТЬ
					|					втКомпьютеры.ОсновноеСредство
					|				ИЗ
					|					втКомпьютеры КАК втКомпьютеры)) КАК РазмещениеОбъектовОстатки";
					
					
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(ДополнительныеСвойства.ДляПроведения.Ссылка.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Регистратор", ДополнительныеСвойства.ДляПроведения.Ссылка);
	Запрос.УстановитьПараметр("Период", ДополнительныеСвойства.ДляПроведения.Дата);
	
	Возврат Запрос.Выполнить().Выгрузить();
	 
КонецФункции	 


Функция ПолучитьТаблицуЛицензииОрганизации(ДополнительныеСвойства) Экспорт
	
	Действие = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Действие при деинсталяции");
	ТаблицаРаспределения = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРаспределениеЛицензий;
	
	ТаблицаЛицензииОрганизации = ТаблицаРаспределения.СкопироватьКолонки();
	Для каждого Строка Из ТаблицаРаспределения Цикл
		
		НайденнаяСтрока = Строка.Лицензия.ДополнительныеРеквизиты.Найти(Действие, "Свойство");
		
		Если НайденнаяСтрока.Значение = Перечисления.ВариантДействияПриДеинсталяции.Освобождать Тогда
			
			НоваяСтрока = ТаблицаЛицензииОрганизации.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			
			НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Приход;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Возврат ТаблицаЛицензииОрганизации;
	
КонецФункции //ПолучитьТаблицуЛицензииОрганизации	


Функция ПолучитьТаблицуМестонахождениеОС(ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеОСОсновныеСредства.Ссылка КАК Регистратор,
	               |	СписаниеОСОсновныеСредства.Ссылка.Дата КАК Период,
	               |	СписаниеОСОсновныеСредства.ОсновноеСредство,
	               |	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	               |	ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка) КАК ОСВладелец,
	               |	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК ПодразделениеОрганизации,
	               |	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК МОЛ,
	               |	ЗНАЧЕНИЕ(Справочник.Помещение.ПустаяСсылка) КАК Помещение
	               |ИЗ
	               |	Документ.СписаниеОС.ОсновныеСредства КАК СписаниеОСОсновныеСредства
	               |ГДЕ
	               |	СписаниеОСОсновныеСредства.Ссылка = &Ссылка";
	
	 Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	 Возврат(Запрос.Выполнить().Выгрузить());
	 
 КонецФункции // ПолучитьТаблицуКомплектующих()
 
 // <Описание функции>
 //
 // Параметры
 //  <Параметр1>  - <Тип.Вид> - <описание параметра>
 //                 <продолжение описания параметра>
 //  <Параметр2>  - <Тип.Вид> - <описание параметра>
 //                 <продолжение описания параметра>
 //
 // Возвращаемое значение:
 //   <Тип.Вид>   - <описание возвращаемого значения>
 //
 Функция ПолучитьТаблицуОтключенныеКомпьютеры(ДополнительныеСвойства) Экспорт
 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеОСОсновныеСредства.Ссылка КАК Регистратор,
	               |	СписаниеОСОсновныеСредства.Ссылка.Дата КАК Период,
	               |	СписаниеОСОсновныеСредства.ОсновноеСредство КАК ИдентификаторКомпьютера,
	               |	ИСТИНА КАК Отключен
	               |ИЗ
	               |	Документ.СписаниеОС.ОсновныеСредства КАК СписаниеОСОсновныеСредства
	               |ГДЕ
	               |	СписаниеОСОсновныеСредства.Ссылка = &Ссылка";
	
	 Запрос.УстановитьПараметр("Ссылка", ДополнительныеСвойства.ДляПроведения.Ссылка);
	 Возврат(Запрос.Выполнить().Выгрузить());
 
 КонецФункции // ПолучитьТаблицуОтключенныеКомпьютеры()
  

// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Дата          - Дата - дата документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "") Экспорт
	
	ДанныеДокумента = Новый Структура("Номер,Дата,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = Найти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	НомерНаПечать = ДанныеДокумента.Номер;
	СтандартнаяОбработка = Истина;
	ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(НомерНаПечать, СтандартнаяОбработка);
	Если СтандартнаяОбработка Тогда
		НомерНаПечать = ДанныеДокумента.Номер;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 № %2 от %3'"),
		НазваниеДокумента, НомерНаПечать, Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	
КонецФункции

Функция ПечатьСписаниеОС(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПараметрыПечати_СписаниеОС";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СписаниеОС.Ссылка,
	               |	СписаниеОС.ОсновныеСредства.(
	               |		НомерСтроки,
	               |		ОсновноеСредство.Наименование КАК ОсновноеСредство,
	               |		Комментарий
	               |	),
	               |	СписаниеОС.Дата,
	               |	СписаниеОС.Номер,
	               |	СписаниеОС.Комментарий,
	               |	СписаниеОС.Ответственный
	               |ИЗ
	               |	Документ.СписаниеОС КАК СписаниеОС
	               |ГДЕ
	               |	СписаниеОС.Ссылка В(&МассивОбъектов)";
				   
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Шапка = Запрос.Выполнить().Выбрать();
	
	Макет = УправлениеПечатью.ПолучитьМакет("Документ.СписаниеОС.ПФ_MXL_СписаниеОС");
	
	ПервыйДокумент = Истина;
	Пока Шапка.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			//Все документы необходимо выводить  на разных страницах
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		//Запомним номер строки, с которой начали выводить текущий документ
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, "Списание основных средств");
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("Комментарий", Шапка.Комментарий);
		ДанныеПечати.Вставить("Ответственный", Шапка.Ответственный);
		
		ТаблицаОсновныеСредства = Шапка.ОсновныеСредства.Выгрузить();
		
		МассивОбластейМакета = Новый Массив;
		
		//Если ИмяМакета = "СписаниеОС" Тогда
		//	МассивОбластейМакета.Добавить("ЗаголовокСчета");
		//КонецЕсли;
		МассивОбластейМакета.Добавить("Заголовок");
		//МассивОбластейМакета.Добавить("Поставщик");
		//МассивОбластейМакета.Добавить("Покупатель");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("Строка");
		//МассивОбластейМакета.Добавить("Итого");
		//МассивОбластейМакета.Добавить("ИтогоНДС");
		//МассивОбластейМакета.Добавить("СуммаПрописью");
		МассивОбластейМакета.Добавить("Подвал");
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Строка" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			Иначе
				Для Каждого СтрокаТаблицы Из ТаблицаОсновныеСредства Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;

		//В табличном документе необходимо задать имя области,  в которую был
		// выведен объект. Необходимо для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.СписаниеОС";
	КомандаПечати.Идентификатор = "СписаниеОС";
	КомандаПечати.Представление = НСтр("ru = 'Списание основных средств'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;	
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	НужноПечататьМакет = УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СписаниеОС");
	Если НужноПечататьМакет Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"СписаниеОС",
		НСтр("ru = 'Списание основных средств'"),
		ПечатьСписаниеОС(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.СписаниеОС.ПФ_MXL_СписаниеОС");
	КонецЕсли;	



КонецПроцедуры
