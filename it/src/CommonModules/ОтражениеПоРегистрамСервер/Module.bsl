////////////////////////////////////////////////////////////////////////////////
// УПРАВЛЕНИЕ ПРОВЕДЕНИЕМ

// Выполняет инициализацию дополнительных свойств для проведения документа.
//
Процедура ДополнительныеСвойстваДляПроведения(ДокументСсылка, СтруктураДополнительныеСвойства, РежимПроведения = Неопределено) Экспорт
	
	// В структуре "ДополнительныеСвойства" создаются свойства с ключами "ТаблицыДляДвижений", "ДляПроведения", "УчетнаяПолитика".
	
	// "ТаблицыДляДвижений" - структура, которая будет содержать таблицы значений с данными для выполнения движений.
	СтруктураДополнительныеСвойства.Вставить("ТаблицыДляДвижений", Новый Структура);
	
	// "ДляПроведения" - структура, содержащая свойства и реквизиты документа, необходимые для проведения.
	СтруктураДополнительныеСвойства.Вставить("ДляПроведения", Новый Структура);
	
	// Структура, содержащая ключ с именем "МенеджерВременныхТаблиц", в значении которого хранится менеджер временных таблиц.
	// Содержит для каждой временной таблицы ключ (имя временной таблицы) и значение (признак наличия записей во временной таблице).
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("СтруктураВременныеТаблицы", Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц));
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("МетаданныеДокумента", 	ДокументСсылка.Метаданные());
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("РежимПроведения", 		РежимПроведения);
	
	// Запрос, получающий данные документа.
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	_Документ_.Ссылка КАК Ссылка,
	|	_Документ_.Номер КАК Номер,
	|	_Документ_.Дата КАК Дата,
	|   " + ?(СтруктураДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено, "_Документ_.Организация" , "ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)") + " КАК Организация,
	|	_Документ_.МоментВремени КАК МоментВремени,
	|	_Документ_.Представление КАК Представление
	|ИЗ
	|	Документ." + СтруктураДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Имя + " КАК _Документ_
	|ГДЕ
	|	_Документ_.Ссылка = &ДокументСсылка");
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Формирование ключей, содержащих данные документа.
	Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
		
		СтруктураДополнительныеСвойства.ДляПроведения.Вставить(Колонка.Имя);
		
	КонецЦикла;
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Заполнение значений для ключей, содержащих данные документа.
	ЗаполнитьЗначенияСвойств(СтруктураДополнительныеСвойства.ДляПроведения, ВыборкаИзРезультатаЗапроса);
	
	// Определение и установка значения момента, на который должен быть выполнен контроль документа.
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("МоментКонтроля", Дата('00010101'));
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("ПериодКонтроля", Дата("39991231"));
	
КонецПроцедуры // ДополнительныеСвойстваДляПроведения()

//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ ДВИЖЕНИЙ РЕГИСТРОВ

// Выполняет движения регистра накопления.
//
Процедура ОтразитьКомплектациюОС(ТаблицаДвижений, ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Если Отказ
	 ИЛИ ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияРегистра = Движения.КомплектующиеОС;
	ДвиженияРегистра.Загрузить(ТаблицаДвижений);
	ДвиженияРегистра.Записать();
	
КонецПроцедуры

// Выполняет движения регистра сведений.
//
Процедура ОтразитьМестонахождениеОС(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаМестонахождениеОС;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.МестонахождениеОС.Записывать = Истина;
	Движения.МестонахождениеОС.Загрузить(Таблица);
	
КонецПроцедуры

// <Описание процедуры>
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура ОтразитьОтключенныеКомпьютеры(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОтключенныеКомпьютеры;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ОтключенныеКомпьютеры.Записывать = Истина;
	Движения.ОтключенныеКомпьютеры.Загрузить(Таблица);

КонецПроцедуры // ОтразитьОтключенныеКомпьютеры()


// Выполняет движения регистра сведений.
//
Процедура ОтразитьПервоначальныеСведенияОС(ТаблицаДвижений, ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Если Отказ
	 ИЛИ ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияРегистра = Движения.ПервоначальныеСведенияОС;
	ДвиженияРегистра.Загрузить(ТаблицаДвижений);
	ДвиженияРегистра.Записать();
	
КонецПроцедуры

// Выполняет движения регистра накопления.
//
Процедура ОтразитьРазмещениеОбъектов(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеОбъектов;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.РазмещениеОбъектов.Записывать = Истина;
	Движения.РазмещениеОбъектов.Загрузить(Таблица);
	
КонецПроцедуры

// Выполняет движения регистра накопления.
//
Процедура ОтразитьЗакупкуЛицензий(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗакупки;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.Закупки.Записывать = Истина;
	Движения.Закупки.Загрузить(Таблица);
	
КонецПроцедуры //ОтразитьЗакупкуЛицензий

// Выполняет движения регистра сведений.
//
Процедура ОтразитьПоддержкиЛицензий(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаПоддержкаЛицензий;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ПоддержкаЛицензий.Записывать = Истина;
	Движения.ПоддержкаЛицензий.Загрузить(Таблица);
	
КонецПроцедуры

// Выполняет движения регистра сведений.
//
Процедура ОтразитьСписаниеОС(ТаблицаДвижений, ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Если Отказ
	 ИЛИ ТаблицаДвижений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияРегистра = Движения.СписаныеОС;
	ДвиженияРегистра.Загрузить(ТаблицаДвижений);
	ДвиженияРегистра.Записать();
	
КонецПроцедуры


// Процедура записывает движения документа. Дополнительно происходит копирование параметров
// в модули наборов записей для выполнения регистрации изменений в движениях.
// Процедура вызывается из модуля документов при проведении.
//
Процедура ЗаписатьНаборыЗаписей(Объект) Экспорт
	Перем РегистрыДляКонтроля;

	// Регистры, для которых будут рассчитаны таблицы изменений движений.
	Если Объект.ДополнительныеСвойства.ДляПроведения.Свойство("РегистрыДляКонтроля", РегистрыДляКонтроля) Тогда
		Для Каждого НаборЗаписей Из РегистрыДляКонтроля Цикл
			Если НаборЗаписей.Записывать Тогда

				// Установка флага регистрации изменений в наборе записей.
				НаборЗаписей.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", Истина);
				НаборЗаписей.ДополнительныеСвойства.Вставить("ЭтоНовый", Объект.ДополнительныеСвойства.ЭтоНовый);

				// Структура для передачи данных в модули наборов записей.
				НаборЗаписей.ДополнительныеСвойства.Вставить("ДляПроведения", 
						Новый Структура("СтруктураВременныеТаблицы", Объект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы));

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Объект.Движения.Записать();

КонецПроцедуры


// Функция формирует массив имен регистров, по которым документ имеет движения.
// Вызывается при подготовке записей к регистрации движений.
//
Функция ПолучитьМассивИспользуемыхРегистров(Регистратор, Движения, МассивИсключаемыхРегистров = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);

	Результат = Новый Массив;
	МаксимумТаблицВЗапросе = 256;

	СчетчикТаблиц   = 0;
	СчетчикДвижений = 0;

	ВсегоДвижений = Движения.Количество();
	ТекстЗапроса  = "";
	Для Каждого Движение Из Движения Цикл

		СчетчикДвижений = СчетчикДвижений + 1;

		ПропуститьРегистр = МассивИсключаемыхРегистров <> Неопределено
							И МассивИсключаемыхРегистров.Найти(Движение.Имя) <> Неопределено;

		Если Не ПропуститьРегистр Тогда

			Если СчетчикТаблиц > 0 Тогда

				ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ
				|";

			КонецЕсли;

			СчетчикТаблиц = СчетчикТаблиц + 1;

			ТекстЗапроса = ТекстЗапроса + 
			"
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|""" + Движение.Имя + """ КАК ИмяРегистра
			|
			|ИЗ " + Движение.ПолноеИмя() + "
			|
			|ГДЕ Регистратор = &Регистратор
			|";

		КонецЕсли;

		Если СчетчикТаблиц = МаксимумТаблицВЗапросе Или СчетчикДвижений = ВсегоДвижений Тогда

			Запрос.Текст  = ТекстЗапроса;
			ТекстЗапроса  = "";
			СчетчикТаблиц = 0;

			Если Результат.Количество() = 0 Тогда

				Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяРегистра");

			Иначе

				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					Результат.Добавить(Выборка.ИмяРегистра);
				КонецЦикла;

			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции


// Процедура выполняет пордготовку наборов записей документа к записи движений.
// 1. Очищает наборы записей от "старых записей" (ситуация возможна только в толстом клиенте)
// 2. Взводит флаг записи у наборов, по которым документ имеет движения
// Вызывается из модуля документов при проведении.
//
Процедура ПодготовитьНаборыЗаписейКРегистрацииДвижений(Объект) Экспорт

	Для Каждого НаборЗаписей Из Объект.Движения Цикл

		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Очистить();
		КонецЕсли;

	КонецЦикла;

	Если Не Объект.ДополнительныеСвойства.ЭтоНовый Тогда

		// Регистры, движения по которым формируются не из модуля менеджера документа.
		ИсключаемыеРегистры = Новый Массив;

		МассивИменРегистров = ПолучитьМассивИспользуемыхРегистров(Объект.Ссылка,
				Объект.ДополнительныеСвойства.ДляПроведения.МетаданныеДокумента.Движения,
				ИсключаемыеРегистры);

		Для Каждого ИмяРегистра Из МассивИменРегистров Цикл
			Объект.Движения[ИмяРегистра].Записывать = Истина;
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры


// Выполняет движения регистра накопления.
//
Процедура ОтразитьРаспределенияЛицензий(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРаспределениеЛицензий;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.РазмещениеЛицензий.Записывать = Истина;
	Движения.РазмещениеЛицензий.Загрузить(Таблица);
	
КонецПроцедуры

// Выполняет движения регистра накопления.
//
Процедура ОтразитьЛицензииОрганизации(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица	= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЛицензииОрганизации;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ЛицензииОрганизации.Записывать = Истина;
	Движения.ЛицензииОрганизации.Загрузить(Таблица);
	
КонецПроцедуры //ОтразитьЛицензииОрганизации


Процедура ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства) Экспорт

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();

КонецПроцедуры


Процедура УстановитьРежимПроведения(ДокументОбъект, РежимЗаписи, РежимПроведения) Экспорт

	Если ДокументОбъект.Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		РежимПроведения = РежимПроведенияДокумента.Неоперативный;
	КонецЕсли;

КонецПроцедуры


// Функция вызывается из модулей наборов записей для проверки необходимости
// контроля изменений движений в регистре.
//
Функция РассчитыватьИзменения(ДополнительныеСвойстваНабораЗаписей) Экспорт
	Перем РассчитыватьИзменения;

	Возврат ДополнительныеСвойстваНабораЗаписей.Свойство("РассчитыватьИзменения", РассчитыватьИзменения)
		И РассчитыватьИзменения;

КонецФункции


// Функция проверяет наличие изменений в таблице регистра.
//
Функция ЕстьИзмененияВТаблице(СтруктураДанных, Ключ)
	Перем ЕстьИзменения;

	Возврат СтруктураДанных.Свойство(Ключ, ЕстьИзменения) И ЕстьИзменения;

КонецФункции


// <Описание процедуры>
//
// Параметры
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
Процедура ВыполнитьКонтрольРезультатовПроведения(Объект, Отказ) Экспорт

	Если Объект.ДополнительныеСвойства.ДляПроведения.РегистрыДляКонтроля.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДанныеТаблиц    = Объект.ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	ПакетЗапросов   = Новый Запрос;
	МассивКонтролей = Новый Массив;
	ТекстЗапроса    = "";

	Если ЕстьИзмененияВТаблице(ДанныеТаблиц,"ДвиженияРазмещениеЛицензийИзменение") Тогда

		МассивКонтролей.Добавить(Врег("РазмещениеЛицензий"));
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаОстатков.Организация,
		|	ТаблицаОстатков.ПодразделениеОрганизации,
		|	ТаблицаОстатков.ИдентификаторУстановлено,
		|	ТаблицаОстатков.ИдентификаторПрограммы,
		|	ТаблицаОстатков.ИдентификаторКомпьютера,
		|	ТаблицаОстатков.Лицензия,
		|	ТаблицаОстатков.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.РазмещениеЛицензий.Остатки(
		|			,
		|			(Организация, ПодразделениеОрганизации, ИдентификаторУстановлено, ИдентификаторПрограммы, ИдентификаторКомпьютера, Лицензия) В
		|				(ВЫБРАТЬ
		|					Таблица.Организация,
		|					Таблица.ПодразделениеОрганизации,
		|					Таблица.ИдентификаторУстановлено,
		|					Таблица.ИдентификаторПрограммы,
		|					Таблица.ИдентификаторКомпьютера,
		|					Таблица.Лицензия
		|				ИЗ
		|					ДвиженияРазмещениеЛицензийИзменение КАК Таблица)) КАК ТаблицаОстатков
		|ГДЕ
		|	ТаблицаОстатков.КоличествоОстаток < 0;
		|///////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	// Контроль отрицательных остатков ЛицензииОрганизации.
	Если ЕстьИзмененияВТаблице(ДанныеТаблиц,"ДвиженияЛицензииОрганизацииИзменение") Тогда         

		МассивКонтролей.Добавить(Врег("ЛицензииОрганизации"));
		ТекстЗапроса = ТекстЗапроса + 
		"
		|ВЫБРАТЬ
		|	ТаблицаОстатков.Организация      		КАК Организация,
		|	ТаблицаОстатков.ИдентификаторПрограммы	КАК ИдентификаторПрограммы,
		|	ТаблицаОстатков.Лицензия          		КАК Лицензия,
		|	ТаблицаОстатков.КоличествоОстаток 		КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ЛицензииОрганизации.Остатки(,
		|			(Организация, ИдентификаторПрограммы, Лицензия) В
		|				(ВЫБРАТЬ
		|					Таблица.Организация,
		|					Таблица.ИдентификаторПрограммы,
		|					Таблица.Лицензия
		|				ИЗ
		|					ДвиженияЛицензииОрганизацииИзменение КАК Таблица)
		|	) КАК ТаблицаОстатков
		|
		|ГДЕ
		|	ТаблицаОстатков.КоличествоОстаток < 0;
		|///////////////////////////////////////////////////////////////////
		|";
	КонецЕсли;

	// Контроль отрицательных остатков РазмещениеОбъектов.
	Если ЕстьИзмененияВТаблице(ДанныеТаблиц,"ДвиженияРазмещениеОбъектовИзменение") Тогда

		МассивКонтролей.Добавить(Врег("РазмещениеОбъектов"));
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаОстатков.ИдентификаторДомена КАК ИдентификаторДомена,
		|	ТаблицаОстатков.ИдентификаторКомпьютера КАК ИдентификаторКомпьютера,
		|	ТаблицаОстатков.ИдентификаторПрограммы КАК ИдентификаторПрограммы,
		|	ТаблицаОстатков.ОбъектКомпьютера КАК ОбъектКомпьютера,
		|	ТаблицаОстатков.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.РазмещениеОбъектов.Остатки(
		|			,
		|			(ИдентификаторДомена, ИдентификаторКомпьютера, ИдентификаторПрограммы, ОбъектКомпьютера) В
		|				(ВЫБРАТЬ
		|					Таблица.ИдентификаторДомена,
		|					Таблица.ИдентификаторКомпьютера,
		|					Таблица.ИдентификаторПрограммы,
		|					Таблица.ОбъектКомпьютера
		|				ИЗ
		|					ДвиженияРазмещениеОбъектовИзменение КАК Таблица)) КАК ТаблицаОстатков
		|ГДЕ
		|	ТаблицаОстатков.КоличествоОстаток < 0";
	КонецЕсли;

	
	Если МассивКонтролей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПакетЗапросов.Текст = ТекстЗапроса;
	ПакетЗапросов.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	МассивРезультатов = ПакетЗапросов.ВыполнитьПакет();

	Итератор = -1;
	Для Каждого Результат Из МассивРезультатов Цикл

		Итератор = Итератор + 1;
		Если Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;

		ИмяКонтроля = МассивКонтролей[Итератор];

		Если ИмяКонтроля = Врег("РазмещениеЛицензий") Тогда

			СообщитьОбОшибкахПроведенияПоРегиструРазмещениеЛицензий(Объект, Отказ, Результат);

		ИначеЕсли ИмяКонтроля = Врег("ЛицензииОрганизации") Тогда

			СообщитьОбОшибкахПроведенияПоРегиструЛицензииОрганизации(Объект, Отказ, Результат);

		ИначеЕсли ИмяКонтроля = Врег("РазмещениеОбъектов") Тогда

			СообщитьОбОшибкахПроведенияПоРегиструРазмещениеОбъектов(Объект, Отказ, Результат);

		Иначе

			ВызватьИсключение НСтр("ru = 'Ошибка контроля проведения!'");

		КонецЕсли;
	КонецЦикла;

	Если Отказ Тогда

		Если Объект.ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ТекстСообщения = НСтр("ru = 'Проведение не выполнено '");
		Иначе
			ТекстСообщения = НСтр("ru = 'Отмена проведения не выполнена '");
		КонецЕсли;

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения + Строка(Объект), Объект);

	КонецЕсли;

КонецПроцедуры // ВыполнитьКонтрольРезультатовПроведения()


Процедура СообщитьОбОшибкахПроведенияПоРегиструРазмещениеЛицензий(Объект, Отказ, РезультатЗапроса)

	ШаблонСообщения = НСтр("ru = 'Лицензий %1 
		|Недостаточно для распределения в количестве %2 ед.'");

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Строка(Выборка.Лицензия), Строка(-Выборка.КоличествоОстаток));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, ,, Отказ);

	КонецЦикла;

КонецПроцедуры


Процедура СообщитьОбОшибкахПроведенияПоРегиструЛицензииОрганизации(Объект, Отказ, РезультатЗапроса)

	ШаблонСообщения = НСтр("ru = 'Лицензий %1 
		|Недостаточно остатка в количестве %2 ед.'");

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Строка(Выборка.Лицензия), Строка(-Выборка.КоличествоОстаток));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, ,, Отказ);

	КонецЦикла;

КонецПроцедуры


Процедура СообщитьОбОшибкахПроведенияПоРегиструРазмещениеОбъектов(Объект, Отказ, РезультатЗапроса)

	ШаблонСообщения = НСтр("ru = 'Объект %1 
		|Недостаточно остатка в количестве %2 ед.'");

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Строка(Выборка.ОбъектКомпьютера), Строка(-Выборка.КоличествоОстаток));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект, ,, Отказ);

	КонецЦикла;

КонецПроцедуры
