
// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  ДокументОбъект             - объект проводимого документа, 
//  СтруктураОбязательныхПолей - структура, содержащая имена полей, которые собственно и надо проверить.
//  Отказ                      - флаг отказа в проведении.
//  Заголовок                  - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапкиДокумента(ДокументОбъект, СтруктураОбязательныхПолей, Отказ, Заголовок) Экспорт
	
	//ПроверитьПринадлежностьКВидамУчета();

	МетаданныеРеквизиты = ДокументОбъект.Метаданные().Реквизиты;
	
	Для каждого КлючЗначение Из СтруктураОбязательныхПолей Цикл

		Значение = ДокументОбъект[КлючЗначение.Ключ];
		Если НЕ ЗначениеЗаполнено(Значение) Тогда

			Если НЕ ЗначениеЗаполнено(КлючЗначение.Значение) Тогда
				ПредставлениеРеквизита = МетаданныеРеквизиты[КлючЗначение.Ключ].Представление();
				СтрокаСообщения = "Не заполнено значение реквизита """ + СокрЛП(ПредставлениеРеквизита) + """!";
			Иначе
				СтрокаСообщения = КлючЗначение.Значение;
			КонецЕсли;
			//ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(СтрокаСообщения, Отказ, Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеШапкиДокумента()

// Проверяет правильность заполнения строк табличной части документа.
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
//
// Параметры:
//  ДокументОбъект             - объект проводимого документа, 
//  ИмяТабличнойЧасти          - табличная часть документа,
//  СтруктураОбязательныхПолей - структура, содержащая имена полей, которые собственно и надо проверить.
//  Отказ                      - флаг отказа в проведении.
//  Заголовок                  - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеТабличнойЧасти(ДокументОбъект, ИмяТабличнойЧасти, СтруктураОбязательныхПолей, 
                                                    Отказ, Заголовок) Экспорт

	ПредставлениеТабличнойЧасти = ДокументОбъект.Метаданные().ТабличныеЧасти[ИмяТабличнойЧасти].Представление();
	ТабличнаяЧасть = ДокументОбъект[ИмяТабличнойЧасти];
	МетаданныеРеквизиты = ДокументОбъект.Метаданные().ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты;

	// Цикл по строкам табличной части.
	Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл

		СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(СтрокаТаблицы.НомерСтроки) +
		                               """ табличной части """ + ПредставлениеТабличнойЧасти + """: ";

		// Цикл по проверяемым полям
		Для каждого КлючЗначение Из СтруктураОбязательныхПолей Цикл

			Значение = СтрокаТаблицы[КлючЗначение.Ключ];
			Если НЕ ЗначениеЗаполнено(Значение) Тогда

				Если НЕ ЗначениеЗаполнено(КлючЗначение.Значение) Тогда
					ПредставлениеРеквизита = МетаданныеРеквизиты[КлючЗначение.Ключ].Представление();
					СтрокаСообщения = "Не заполнено значение реквизита """ + СокрЛП(ПредставлениеРеквизита) + """!";
				Иначе
					СтрокаСообщения = КлючЗначение.Значение;
				КонецЕсли;
				//ОбщегоНазначенияКлиентСервер.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения, Отказ, Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщения);

			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧасти()

// Проверяет дубли в табличной части
//
// Параметры:
//  СтруктураОбязательныхПолей - структура, содержащая имена полей, которые собственно и надо проверить.
//  Отказ                      - флаг отказа в проведении.
//  Заголовок                  - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьДублиТабличнойЧасти(СтруктураШапкиДокумента, Отказ, Заголовок, КолонкаПроверки, ИмяТабличнойЧасти) Экспорт
	
	ТЗ_Проверки = СтруктураШапкиДокумента.Ссылка[ИмяТабличнойЧасти].Выгрузить();
	ТЗ_Копия 	= ТЗ_Проверки.Скопировать();
	
	КЧ = Новый КвалификаторыЧисла(1);
	Массив = Новый Массив;
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,КЧ);
	
	Кол = ТЗ_Копия.Колонки.Добавить("КоличествоДублей",ОписаниеТиповЧ);
	ТЗ_Копия.ЗаполнитьЗначения(1,"КоличествоДублей");
	ТЗ_Копия.Свернуть(КолонкаПроверки,"КоличествоДублей");
	
	Для Каждого Стр_ТЗ Из ТЗ_Копия Цикл
		Если Стр_ТЗ.КоличествоДублей > 1 Тогда
			ТекстОшибки = "";
			
			ОтборД = Новый Структура();
			ОтборД.Вставить(КолонкаПроверки,Стр_ТЗ[КолонкаПроверки]);
			НайденыДубли = ТЗ_Проверки.НайтиСтроки(ОтборД);
			
			Нм=1;
			Для Каждого Стр_Дублей Из НайденыДубли Цикл
				Если Нм = 1 Тогда
					ТекстОшибки = СтруктураШапкиДокумента.Ссылка.Метаданные().ТабличныеЧасти[ИмяТабличнойЧасти].Реквизиты.Найти(КолонкаПроверки).Синоним 
					+  ": " + Стр_ТЗ[КолонкаПроверки] +  " (в строке № " + Стр_Дублей.НомерСтроки + ") " + " дублируется в строках: ";
				Иначе
					Если НайденыДубли.Количество() = (Нм) Тогда
						ТекстОшибки = ТекстОшибки + Стр_Дублей.НомерСтроки;
					Иначе
						ТекстОшибки = ТекстОшибки + "№ " + Стр_Дублей.НомерСтроки + ", ";
					КонецЕсли;
				КонецЕсли;
				Нм = Нм + 1;
			КонецЦикла;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПроверитьЗаполнениеТабличнойЧастиТовары()


// Процедура проверяет дубли строк в табличной части
// Параметры:
//	Объект - проверяемый ДокументОбъект
//	ИмяТЧ - имя проверяемой табличной части
//	КлючевыеРеквизиты - массив имен реквизитов, по которым определяется уникальность строки
//	Отказ - отказ продолжения операции
Процедура ПроверитьНаличиеДублейСтрокТЧ(Объект,ИмяТЧ,КлючевыеРеквизиты,Отказ, ПредставлениеТЧ = "", УдалитьДубли = Ложь) Экспорт
	
	МетаданныеОбъекта = Объект.Метаданные();
	Если ПустаяСтрока(ПредставлениеТЧ) Тогда
		ПредставлениеТЧ   = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Синоним;
	КонецЕсли;
	
	ТекстПоляВыборки = "";
	ТекстПоляСоединения = "";	
    ТекстПоляВыгрузки = "";
	ТекстДляСообщенияОДублях = "";
	Для Каждого СтрМас из КлючевыеРеквизиты Цикл
		ТекстПоляВыборки = ТекстПоляВыборки + "
	|	ТаблицаПроверки." + СтрМас + ",";
		ТекстПоляСоединения = ТекстПоляСоединения + "
	|	И ТаблицаПроверки." + СтрМас + " = ДублирующиесяСтроки."+ СтрМас;
		ТекстПоляВыгрузки = ТекстПоляВыгрузки + СтрМас + ",";
		
		ПредставлениеРеквизита = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Реквизиты[СтрМас].Синоним;
		
		ТекстДляСообщенияОДублях = ТекстДляСообщенияОДублях + """"  + ПредставлениеРеквизита  + """, "
	КонецЦикла;	
	
	ТекстДляСообщенияОДублях = Лев(ТекстДляСообщенияОДублях, СтрДлина(ТекстДляСообщенияОДублях) - 2);
	
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(ТекстПоляВыборки,1);
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(ТекстПоляВыгрузки,1);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	ТаблицаПроверки.НомерСтроки, " +
		ТекстПоляВыборки + "
	|ПОМЕСТИТЬ ТаблицаПроверки
	|ИЗ
	|	&ТаблицаПроверки КАК ТаблицаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаПроверки.НомерСтроки) КАК НомерСтроки,
	|	СУММА(1) КАК КоличествоДублей,"+
		ТекстПоляВыборки + "
	|ПОМЕСТИТЬ ДублирующиесяСтроки
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|
	|СГРУППИРОВАТЬ ПО " +
		ТекстПоляВыборки + "
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроверки.НомерСтроки,
	|	ДублирующиесяСтроки.НомерСтроки КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДублирующиесяСтроки КАК ДублирующиесяСтроки
	|		ПО ТаблицаПроверки.НомерСтроки <> ДублирующиесяСтроки.НомерСтроки " +
	     		ТекстПоляСоединения;
	Запрос.УстановитьПараметр("ТаблицаПроверки",Объект[ИмяТЧ].Выгрузить(,"НомерСтроки," + ТекстПоляВыгрузки));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если КлючевыеРеквизиты.Количество() = 1 Тогда
		ШаблонСообщения = НСтр("ru = 'Данные в строке %НомерСтроки% списка ""%ПредставлениеТЧ%"" совпадают с данными в строке %ПерваяСтрока% по значению поля %НазванияПолей%.'");
	Иначе
		ШаблонСообщения = НСтр("ru = 'Данные в строке %НомерСтроки% списка ""%ПредставлениеТЧ%"" совпадают с данными в строке %ПерваяСтрока% по сочетанию значений полей %НазванияПолей%.'");
	КонецЕсли;	
	
	МассивСтрок = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ УдалитьДубли Тогда
			
			ТекстСообщения =  СтрЗаменить(ШаблонСообщения, "%ПредставлениеТЧ%", ПредставлениеТЧ);
			ТекстСообщения =  СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
			ТекстСообщения =  СтрЗаменить(ТекстСообщения, "%ПерваяСтрока%", Выборка.ПерваяСтрока);
			ТекстСообщения =  СтрЗаменить(ТекстСообщения, "%НазванияПолей%", ТекстДляСообщенияОДублях);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Выборка.НомерСтроки, "НомерСтроки");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект",Отказ);
			
		Иначе	
		
			МассивСтрок.Добавить(Объект[ИмяТЧ].Получить(Выборка.НомерСтроки-1));
			
		КонецЕсли;	
		
	КонецЦикла;
	
	Если УдалитьДубли Тогда
	
		Для каждого Строка Из МассивСтрок Цикл
		
			Объект[ИмяТЧ].Удалить(Строка);	
		
		КонецЦикла; 
	
	КонецЕсли; 
	
	
КонецПроцедуры


// Функция формирует представление документа поступления.
//
// Параметры:
// Номер - Строка - Номер документа поступления
// Дата  - Дата - Дата документа поступления
//
// Возвращаемое значение:
// Строка - Представление документа поступления
//
Функция ПредставлениеДокументаПоступления(Номер, Дата) Экспорт
	
	Возврат "№ " + СокрЛП(Номер) + " от " + Формат(Дата, "ДФ=dd.MM.yyyy") + " г.";
	
КонецФункции // ПредставлениеДокументаПоступления


// Функция формирует текст документа продажи.
//
// Параметры:
//	ОсновноеСредство - Текущий элемент справочника ОсновныеСредства
//	Организация    - СправочникСсылка.Организации - Организация, для которой формируется документ поступления (не заполняется из справочника ОсновныеСредства)
//	Поле - ПолеФормы - Поле формы для документа поступления
//	НеТребуется - Булево - Истина - для элемента справочника не требуется вводить документ поступления
//
// Возвращаемые параметры:
//	Гиперссылка - Булево - Признак показа гипер-ссылки для поля документа поступления
//	ДокументПоступления - ДокументСсылка.ПоступлениеОС - Найденный документ поступления
//
// Возвращаемое значение:
//  Строка - Текст документа поступления
//
Функция ТекстДокументаПоступления(ОсновноеСредство, Организация = Неопределено, Поле = Неопределено, НеТребуется = Ложь, Гиперссылка = Истина, ДокументПоступления = Неопределено) Экспорт
	
	Перем РеквизитыДокументаПоступления;
	
	ДокументПоступления = Документы.ПоступлениеОС.ДокументПоступления(ОсновноеСредство, РеквизитыДокументаПоступления);
	Если ЗначениеЗаполнено(ДокументПоступления) Тогда
		ТекстДокументаПоступления = ПредставлениеДокументаПоступления(РеквизитыДокументаПоступления.Номер, РеквизитыДокументаПоступления.Дата);
		Гиперссылка = Истина;
		
	ИначеЕсли НеТребуется Тогда
		ТекстДокументаПоступления = "Не требуется";
		Гиперссылка = Ложь;	
		
	ИначеЕсли Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ПоступлениеОС) Тогда
		ТекстДокументаПоступления = "Не введен";
		Гиперссылка = Ложь;
		
	Иначе
		ТекстДокументаПоступления = "Создать документ поступления";
		Гиперссылка = Истина;
		
	КонецЕсли;
	
	Если Поле <> Неопределено Тогда
		Если Не ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ПоступлениеОС) Тогда
			Поле.Гиперссылка = Ложь;
		Иначе
			Если Поле.Гиперссылка <> Гиперссылка Тогда
				Поле.Гиперссылка = Гиперссылка;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстДокументаПоступления;
	
КонецФункции // ТекстДокументаПоступления

