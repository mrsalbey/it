
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ОтражениеПоРегистрамСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ИдентификаторДомена КАК ИдентификаторДомена,
	|	ТаблицаИзменений.ИдентификаторПрограммы КАК ИдентификаторПрограммы,
	|	ТаблицаИзменений.ИдентификаторКомпьютера КАК ИдентификаторКомпьютера,
	|	ТаблицаИзменений.ОбъектКомпьютера КАК ОбъектКомпьютера,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияРазмещениеОбъектовИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ИдентификаторДомена КАК ИдентификаторДомена,
	|		Таблица.ИдентификаторПрограммы КАК ИдентификаторПрограммы,
	|		Таблица.ИдентификаторКомпьютера КАК ИдентификаторКомпьютера,
	|		Таблица.ОбъектКомпьютера КАК ОбъектКомпьютера,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияРазмещениеОбъектовПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ИдентификаторДомена,
	|		Таблица.ИдентификаторПрограммы,
	|		Таблица.ИдентификаторКомпьютера,
	|		Таблица.ОбъектКомпьютера,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РазмещениеОбъектов КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ИдентификаторДомена,
	|	ТаблицаИзменений.ИдентификаторПрограммы,
	|	ТаблицаИзменений.ИдентификаторКомпьютера,
	|	ТаблицаИзменений.ОбъектКомпьютера
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияРазмещениеОбъектовПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРазмещениеОбъектовИзменение", Выборка.Количество > 0);
	
	
	//Выборка = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ОтражениеПоРегистрамСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ИдентификаторДомена			КАК ИдентификаторДомена,
	|	Таблица.ИдентификаторПрограммы      КАК ИдентификаторПрограммы,
	|	Таблица.ИдентификаторКомпьютера     КАК ИдентификаторКомпьютера,
	|	Таблица.ОбъектКомпьютера			КАК ОбъектКомпьютера,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                  КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияРазмещениеОбъектовПередЗаписью
	|ИЗ
	|	РегистрНакопления.РазмещениеОбъектов КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры
