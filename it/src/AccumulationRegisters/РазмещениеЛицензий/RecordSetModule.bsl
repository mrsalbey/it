
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ОтражениеПоРегистрамСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ТаблицаИзменений.ИдентификаторУстановлено КАК ИдентификаторУстановлено,
	|	ТаблицаИзменений.ИдентификаторПрограммы КАК ИдентификаторПрограммы,
	|	ТаблицаИзменений.ИдентификаторКомпьютера КАК ИдентификаторКомпьютера,
	|	ТаблицаИзменений.Лицензия КАК Лицензия,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияРазмещениеЛицензийИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Организация КАК Организация,
	|		Таблица.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|		Таблица.ИдентификаторУстановлено КАК ИдентификаторУстановлено,
	|		Таблица.ИдентификаторПрограммы КАК ИдентификаторПрограммы,
	|		Таблица.ИдентификаторКомпьютера КАК ИдентификаторКомпьютера,
	|		Таблица.Лицензия КАК Лицензия,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияРазмещениеЛицензийПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Организация,
	|		Таблица.ПодразделениеОрганизации,
	|		Таблица.ИдентификаторУстановлено,
	|		Таблица.ИдентификаторПрограммы,
	|		Таблица.ИдентификаторКомпьютера,
	|		Таблица.Лицензия,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.РазмещениеЛицензий КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ПодразделениеОрганизации,
	|	ТаблицаИзменений.ИдентификаторУстановлено,
	|	ТаблицаИзменений.ИдентификаторПрограммы,
	|	ТаблицаИзменений.ИдентификаторКомпьютера,
	|	ТаблицаИзменений.Лицензия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияРазмещениеЛицензийПередЗаписью";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРазмещениеЛицензийИзменение", Выборка.Количество > 0);
	
	
	//Выборка = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ОтражениеПоРегистрамСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Организация  				КАК Организация,
	|	Таблица.ПодразделениеОрганизации	КАК ПодразделениеОрганизации,
	|	Таблица.ИдентификаторУстановлено 	КАК ИдентификаторУстановлено,
	|	Таблица.ИдентификаторПрограммы      КАК ИдентификаторПрограммы,
	|	Таблица.ИдентификаторКомпьютера     КАК ИдентификаторКомпьютера,
	|	Таблица.Лицензия      				КАК Лицензия,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                  КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияРазмещениеЛицензийПередЗаписью
	|ИЗ
	|	РегистрНакопления.РазмещениеЛицензий КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();
	
КонецПроцедуры
